apply plugin: "java"

sourceCompatibility = 1.6
sourceSets {
		main {
				java.srcDirs = [ "src/" ]
		}
		
    test {
        java.srcDir file('test/')
    }
}

project.ext.mainClassName = "de.croggle.Main"
project.ext.androidDir = new File("../android");
project.ext.assetsDir = new File("../android/assets");
project.ext.resDir = new File("../android/res");

task run(dependsOn: classes, type: JavaExec) {
    main = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = project.androidDir
    ignoreExitValue = true
}

task dist(type: Jar) {
    from files(sourceSets.main.output.classesDir)
    from files(sourceSets.main.output.resourcesDir)
    from {configurations.compile.collect {zipTree(it)}}
    from files(project.assetsDir)
    into 'build/assets'
    from files(project.resDir)
    into 'build/res'
 
    manifest {
        attributes 'Main-Class': project.mainClassName
    }
}

test {
	classpath = sourceSets.test.runtimeClasspath
	workingDir = project.androidDir
  // show standard out and standard error of the test JVM(s) on the console
  testLogging.showStandardStreams = true

  // listen to standard out and standard error of the test JVM(s)
  onOutput { descriptor, event ->
     logger.lifecycle("Test: " + descriptor + " produced standard out/err: " + event.message )
  }
  exclude 'de/croggle/test/**'
  exclude 'com/badlogic/gdx/**'
}

dist.dependsOn classes

eclipse {
    project {
        name = appName + "-desktop"
        linkedResource name: 'media/assets', type: '2', location: 'PARENT-1-PROJECT_LOC/android/assets'
        linkedResource name: 'media/res', type: '2', location: 'PARENT-1-PROJECT_LOC/android/res'
    }
}

task afterEclipseImport(description: "Post processing after project generation", group: "IDE") {
  doLast {
    def classpath = new XmlParser().parse(file(".classpath"))
    new Node(classpath, "classpathentry", [ kind: 'src', path: 'media' ]);
    def writer = new FileWriter(file(".classpath"))
    def printer = new XmlNodePrinter(new PrintWriter(writer))
    printer.setPreserveWhitespace(true)
    printer.print(classpath)
  }
}
